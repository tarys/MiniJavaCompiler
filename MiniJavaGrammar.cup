// CUP specification for a simple expression evaluator (w/ actions)

import java_cup.runtime.Symbol;
import nametable.NameTableBuilder;
import nametable.entries.Entry;
import semantic.SemanticAnalyzer;

import java.util.LinkedList;
import java.util.List;


/* allows methods and variable to be placed directly within the generated parser class */
parser code {:
    private NameTableBuilder nameTableBuilder;
    private SemanticAnalyzer semanticAnalyzer;


    protected NameTableBuilder getNameTableBuilder(){
        return this.nameTableBuilder;
    }
    protected SemanticAnalyzer getSemanticAnalyzer(){
        return this.semanticAnalyzer;
    }

    public List<Entry> getNameTable(){
        return this.nameTableBuilder.getNameTable();
    }


    @Override
    public void unrecovered_syntax_error(Symbol cur_token) throws Exception {
        throw new Exception("Syntax error near symbol " + cur_token);
    }
:};

/* Preliminaries to set up and use the scanner.  */
scan with {: return getScanner().next_token(); :};

/* Provides code that will be executed by the parser before it asks for the first token */
init with {:
            nameTableBuilder =  new NameTableBuilder();
            semanticAnalyzer = new  SemanticAnalyzer(nameTableBuilder);
:};

/* Terminals (tokens returned by the scanner). */
// Data types
terminal        INTEGER_TYPE;
terminal        FLOAT_TYPE;
terminal        CHAR_TYPE;
terminal        STRING_TYPE;
terminal        BOOLEAN_TYPE;

// Identifier
terminal    String  	IDENTIFIER;

// Keywords
terminal        CLASS_KEYWORD;
terminal		BOOLEAN_KEYWORD;
terminal		CHAR_KEYWORD;
terminal		ELSE_KEYWORD;
terminal		FLOAT_KEYWORD;
terminal		IF_KEYWORD;
terminal		INT_KEYWORD;
terminal		MAIN_KEYWORD;
terminal		NEW_KEYWORD;
terminal		PUBLIC_KEYWORD;
terminal		RETURN_KEYWORD;
terminal		STATIC_KEYWORD;
terminal		STRING_KEYWORD;
terminal		VOID_KEYWORD;
terminal		WHILE_KEYWORD;

// Operators
terminal        STOP;
terminal        PLUS;
terminal        MINUS;
terminal        UNARY_MINUS;
terminal        TIMES;
terminal        DIVIDE;
terminal        LOWER;
terminal        LOWER_EQUAL;
terminal		GREATER;
terminal		GREATER_EQUAL;
terminal		INSTANCEOF;
terminal		EQUAL;
terminal		NOT_EQUAL;
terminal		EXCLAMATION;
terminal		AND;
terminal		OR;
terminal		ASSIGN;
terminal		SYSTEM_OUT_PRINTLN;
terminal		SYSTEM_IN_READ;
terminal		BREAK;

// Delimiters
terminal		SEMICOLON;
terminal        COMMA;
terminal		LEFT_SQUARE_PARENTHESIS;
terminal		RIGHT_SQUARE_PARENTHESIS;
terminal		LEFT_PARENTHESIS;
terminal		RIGHT_PARENTHESIS;
terminal		LEFT_FIG_PARENTHESIS;
terminal		RIGHT_FIG_PARENTHESIS;

/* Non-terminals */

non terminal                    goal;
non terminal                    compilation_unit;
non terminal    Entry           main_class;
non terminal    List<Entry>     main_class_methods_declaration;
non terminal    Entry           main_method_declaration;
non terminal    List<Entry>     classes_declaration;
non terminal    Entry           class_declaration;
non terminal    List<Entry>     fields_declaration;
non terminal    Entry           field_declaration;
non terminal    List<Entry>     methods_declaration;
non terminal    Entry           method_declaration;
non terminal    List<Entry>     method_params_declaration;
non terminal    List<Entry>     params_list_declaration;
non terminal    Entry           param_declaration;
non terminal    Entry           block;
non terminal    List<Entry>     variables_declaration;
non terminal    Entry           variable_declaration;
non terminal    List<Entry>     statements;
non terminal    Entry           statement;
non terminal                    assignment_statement;
non terminal    Entry           if_statement;
non terminal    Entry           while_statement;
non terminal                    method_call_statement;
non terminal                    output_statement;
non terminal    String          type;
non terminal    String          reference_type;
/* as we actually neither call method nor calculating expressions we just store String representation of result type */
non terminal    String          expression;
non terminal    List<String>    actual_params_list;

/* Precedences */
precedence left     ASSIGN;
precedence left     OR;
precedence left     AND;
precedence left     EQUAL, NOT_EQUAL;
precedence left     INSTANCEOF, LOWER,LOWER_EQUAL, GREATER, GREATER_EQUAL;
precedence left     PLUS, MINUS;
precedence left     TIMES, DIVIDE;
precedence right    EXCLAMATION, UNARY_MINUS, NEW_KEYWORD;
precedence left     STOP;
precedence left     LEFT_PARENTHESIS, RIGHT_PARENTHESIS;

/* The grammar */
start with goal;

goal                            ::= compilation_unit;

compilation_unit                ::= main_class:mainClass
                                   {:
                                        List<Entry> classes = new LinkedList<Entry>();
                                        classes.add(mainClass);
                                        parser.getNameTableBuilder().declareProgram(classes);
                                   :}
                                  | main_class:mainClass classes_declaration:classes
                                   {:
                                        classes.add(mainClass);
                                        parser.getNameTableBuilder().declareProgram(classes);
                                   :}
                                  | classes_declaration:classes main_class:mainClass
                                   {:
                                        classes.add(mainClass);
                                        parser.getNameTableBuilder().declareProgram(classes);

                                   :}
                                  | classes_declaration:classesListBefore main_class:mainClass classes_declaration:classesListAfter
                                   {:
                                        List<Entry> classes = classesListBefore;
                                        classes.add(mainClass);
                                        classes.addAll(classesListAfter);
                                        parser.getNameTableBuilder().declareProgram(classes);
                                   :}
                                  ;

main_class                      ::= PUBLIC_KEYWORD CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                        fields_declaration:fieldsList
                                        main_class_methods_declaration:methodsList
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMainClass(name, fieldsList, methodsList);
                                   :}
                                  | PUBLIC_KEYWORD CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                        main_class_methods_declaration:methodsList
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMainClass(name, null, methodsList);
                                   :}
                                    ;

main_class_methods_declaration  ::= main_method_declaration:mainMethod
                                   {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(mainMethod);
                                   :}
                                  | main_method_declaration:mainMethod methods_declaration:methodsList
                                   {:
                                        RESULT = methodsList;
                                        RESULT.add(mainMethod);
                                   :}
                                  | methods_declaration:methodsList main_method_declaration:mainMethod
                                   {:
                                        RESULT = methodsList;
                                        RESULT.add(mainMethod);
                                   :}
                                  | methods_declaration:methodsListBefore main_method_declaration:mainMethod methods_declaration:methodsListAfter
                                   {:
                                        RESULT = methodsListBefore;
                                        RESULT.add(mainMethod);
                                        RESULT.addAll(methodsListAfter);
                                   :}
                                  ;

main_method_declaration         ::= PUBLIC_KEYWORD STATIC_KEYWORD VOID_KEYWORD MAIN_KEYWORD
                                    LEFT_PARENTHESIS STRING_KEYWORD LEFT_SQUARE_PARENTHESIS RIGHT_SQUARE_PARENTHESIS
                                    IDENTIFIER RIGHT_PARENTHESIS LEFT_FIG_PARENTHESIS
                                        block:innerBlock
                                    RIGHT_FIG_PARENTHESIS
                                    {:
                                        RESULT = parser.getNameTableBuilder().declareMainMethod(innerBlock);
                                        parser.getSemanticAnalyzer().checkNotUsedBreak();
                                    :}
                                   ;

classes_declaration             ::= classes_declaration:classesList class_declaration:classDeclaration
                                   {:
                                        classesList.add(classDeclaration);
                                        RESULT = classesList;
                                   :}
                                  | class_declaration:classDeclaration
                                   {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(classDeclaration);
                                   :}
                                  ;

class_declaration               ::= CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                        fields_declaration:fieldsList
                                        methods_declaration:methodsList
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareClass(name, fieldsList, methodsList);
                                   :}
                                  | CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                        fields_declaration:fieldsList
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareClass(name, fieldsList, null);
                                   :}
                                  | CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                        methods_declaration:methodsList
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareClass(name, null, methodsList);
                                   :}
                                  | CLASS_KEYWORD IDENTIFIER:name LEFT_FIG_PARENTHESIS
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareClass(name, null, null);
                                   :}
                                  ;

fields_declaration              ::= fields_declaration:fieldsList field_declaration:field
                                   {:
                                        fieldsList.add(field);
                                        RESULT = fieldsList;
                                   :}
                                  | field_declaration:field
                                   {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(field);
                                   :}
                                  ;

field_declaration               ::= PUBLIC_KEYWORD type:type IDENTIFIER:name ASSIGN expression:expression SEMICOLON
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareField(name, type);
                                        parser.getSemanticAnalyzer().assignmentStatement(name, expression);
                                   :}
                                  | PUBLIC_KEYWORD type:type IDENTIFIER:name SEMICOLON
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareField(name, type);
                                   :}
                                  ;

methods_declaration             ::= methods_declaration:methodsList method_declaration:method
                                   {:
                                        methodsList.add(method);
                                        RESULT = methodsList;
                                   :}
                                  | method_declaration:method
                                   {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(method);
                                   :}
                                  ;

method_declaration              ::= PUBLIC_KEYWORD type:returnType IDENTIFIER:name method_params_declaration:paramsList
                                    LEFT_FIG_PARENTHESIS
                                        block:innerBlock
                                        RETURN_KEYWORD expression:expression SEMICOLON
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMethod(name, returnType, paramsList, innerBlock);
                                        parser.getSemanticAnalyzer().methodDeclaration(returnType, innerBlock, expression);
                                   :}
                                  | PUBLIC_KEYWORD type:returnType IDENTIFIER:name method_params_declaration:paramsList
                                    LEFT_FIG_PARENTHESIS
                                        RETURN_KEYWORD expression:expression SEMICOLON
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMethod(name, returnType, paramsList);
                                        parser.getSemanticAnalyzer().methodDeclaration(returnType, expression);
                                   :}
                                  | PUBLIC_KEYWORD VOID_KEYWORD IDENTIFIER:name method_params_declaration:paramsList
                                    LEFT_FIG_PARENTHESIS
                                        block:innerBlock
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMethod(name, "void", paramsList, innerBlock);
                                        parser.getSemanticAnalyzer().methodDeclaration();
                                   :}
                                  | PUBLIC_KEYWORD VOID_KEYWORD IDENTIFIER:name method_params_declaration:paramsList
                                    LEFT_FIG_PARENTHESIS
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareMethod(name, "void", paramsList);
                                        parser.getSemanticAnalyzer().methodDeclaration();
                                   :}
                                  ;

method_params_declaration       ::= LEFT_PARENTHESIS RIGHT_PARENTHESIS
                                  | LEFT_PARENTHESIS params_list_declaration:paramsList RIGHT_PARENTHESIS
                                    {:
                                        RESULT = paramsList;
                                    :}
                                  ;

params_list_declaration         ::= params_list_declaration:paramsList COMMA param_declaration:param
                                    {:
                                        paramsList.add(param);
                                        RESULT = paramsList;
                                    :}
                                  | param_declaration:param
                                    {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(param);
                                    :}
                                  ;

param_declaration               ::= type:type IDENTIFIER:name
                                     {:
                                           RESULT = parser.getNameTableBuilder().declareMethodParameter(name, type);
                                     :}
                                    ;

block                           ::= variables_declaration:variablesEntriesList
                                   {:
                                       RESULT = parser.getNameTableBuilder().declareBlock(variablesEntriesList);
                                   :}
                                  | variables_declaration:variablesEntriesList statements:statements
                                   {:
                                       RESULT = parser.getNameTableBuilder().declareBlock(variablesEntriesList, statements);
                                   :}
                                  | statements:statements
                                   {:
                                       RESULT = parser.getNameTableBuilder().declareBlock(null, statements);
                                   :}
                                  ;

variables_declaration           ::= variables_declaration:variablesEntriesList variable_declaration:variableEntry
                                    {:
                                        variablesEntriesList.add(variableEntry);
                                        RESULT = variablesEntriesList;
                                    :}
                                  | variable_declaration:variableEntry
                                  {:
                                        RESULT = new LinkedList<Entry>();
                                        RESULT.add(variableEntry);
                                  :}
                                  ;

variable_declaration            ::= type:type IDENTIFIER:name SEMICOLON
                                    {:
                                        RESULT = parser.getNameTableBuilder().declareVariable(name, type);
                                    :}
                                  | type:type IDENTIFIER:name ASSIGN expression:expression SEMICOLON
                                    {:
                                        RESULT = parser.getNameTableBuilder().declareVariable(name, type);
                                        parser.getSemanticAnalyzer().assignmentStatement(name, expression);
                                    :}
                                  ;
statements                      ::= statements:statements statement:statement
                                   {:
                                        if (statement != null){
                                            statements.add(statement);
                                        }
                                        RESULT = statements;
                                   :}
                                  | statement:statement
                                   {:
                                        RESULT = new LinkedList<Entry>();
                                        if (statement != null){
                                            RESULT.add(statement);
                                        }
                                   :}
                                  ;

statement                       ::= assignment_statement
                                  | if_statement:statement
                                   {:
                                        RESULT = statement;
                                   :}
                                  | while_statement:statement
                                   {:
                                        RESULT = statement;
                                   :}
                                  | method_call_statement
                                  | output_statement;

assignment_statement            ::= IDENTIFIER:name ASSIGN expression:expression SEMICOLON
                                   {:
                                        parser.getSemanticAnalyzer().assignmentStatement(name, expression);
                                   :}
                                  ;

if_statement                    ::= IF_KEYWORD LEFT_PARENTHESIS expression:conditionExpression RIGHT_PARENTHESIS
                                    LEFT_FIG_PARENTHESIS
                                        block:thenBlock
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareIfStatement(thenBlock);
                                        parser.getSemanticAnalyzer().ifStatement(conditionExpression);
                                   :}
                                  | IF_KEYWORD LEFT_PARENTHESIS expression:conditionExpression RIGHT_PARENTHESIS
                                    LEFT_FIG_PARENTHESIS
                                        block:thenBlock
                                    RIGHT_FIG_PARENTHESIS
                                    ELSE_KEYWORD LEFT_FIG_PARENTHESIS
                                        block:elseBlock
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareIfStatement(thenBlock, elseBlock);
                                        parser.getSemanticAnalyzer().ifStatement(conditionExpression);
                                   :}
                                  ;

while_statement                 ::= WHILE_KEYWORD
                                    LEFT_PARENTHESIS expression:conditionExpression RIGHT_PARENTHESIS
                                    LEFT_FIG_PARENTHESIS
                                        block:innerBlock
                                    RIGHT_FIG_PARENTHESIS
                                   {:
                                        RESULT = parser.getNameTableBuilder().declareWhileStatement(innerBlock);
                                        parser.getSemanticAnalyzer().whileStatement(conditionExpression);
                                   :}
                                  ;

method_call_statement           ::= expression SEMICOLON;


output_statement                ::= SYSTEM_OUT_PRINTLN LEFT_PARENTHESIS expression RIGHT_PARENTHESIS SEMICOLON;

type                            ::= BOOLEAN_KEYWORD
                                    {: RESULT = SemanticAnalyzer.BOOLEAN_TYPE; :}
                                  | CHAR_KEYWORD
                                    {: RESULT = SemanticAnalyzer.CHAR_TYPE; :}
                                  | FLOAT_KEYWORD
                                    {: RESULT = SemanticAnalyzer.FLOAT_TYPE; :}
                                  | INT_KEYWORD
                                    {: RESULT = SemanticAnalyzer.INTEGER_TYPE; :}
                                  | STRING_KEYWORD
                                    {: RESULT = SemanticAnalyzer.STRING_TYPE; :}
                                  | reference_type:value
                                    {: RESULT = value; :}
                                  ;

reference_type                  ::= IDENTIFIER:name
                                    {:
                                        RESULT = parser.getSemanticAnalyzer().isClassDeclared(name);
                                    :};

expression                      ::= expression:className STOP IDENTIFIER:fieldName
                                  {:
                                       RESULT = parser.getSemanticAnalyzer().fieldCallExpression(className, fieldName);
                                  :}
                                  | expression:className STOP IDENTIFIER:methodName LEFT_PARENTHESIS  RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().methodCallExpression(className, methodName);
                                  :}
                                  | expression:className STOP IDENTIFIER:methodName LEFT_PARENTHESIS actual_params_list:actualParameters  RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().methodCallExpression(className, methodName, actualParameters);
                                  :}
                                  | IDENTIFIER:methodName LEFT_PARENTHESIS  RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().methodCallExpression(methodName);
                                  :}
                                  | IDENTIFIER:methodName LEFT_PARENTHESIS actual_params_list:actualParameters  RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().methodCallExpression(methodName, actualParameters);
                                  :}
                                  | expression:arg1 INSTANCEOF expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().instanceofExpression(arg1, arg2);
                                  :}
                                  | SYSTEM_IN_READ LEFT_PARENTHESIS RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().systemReadInExpression();
                                  :}
                                  | EXCLAMATION expression:arg
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().exclamationExpression(arg);
                                  :}
                                  | LEFT_PARENTHESIS expression:arg RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().parenthesisExpression(arg);
                                  :}
                                  | expression:arg1 PLUS expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().plusExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 MINUS expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().minusExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 TIMES expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().timesExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 DIVIDE expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().divideExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 LOWER expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().lowerExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 LOWER_EQUAL expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().lowerEqualExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 GREATER expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().greaterExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 GREATER_EQUAL expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().greaterEqualExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 EQUAL expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().equalExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 NOT_EQUAL expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().notEqualExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 AND expression:arg2
                                  {:
                                       RESULT = parser.getSemanticAnalyzer().andExpression(arg1, arg2);
                                  :}
                                  | expression:arg1 OR expression:arg2
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().orExpression(arg1, arg2);
                                  :}
                                  | INTEGER_TYPE:value
                                  {:
                                        RESULT = SemanticAnalyzer.INTEGER_TYPE;
                                  :}
                                  | FLOAT_TYPE:value
                                  {:
                                        RESULT = SemanticAnalyzer.FLOAT_TYPE;
                                  :}
                                  | BOOLEAN_TYPE:value
                                  {:
                                        RESULT = SemanticAnalyzer.BOOLEAN_TYPE;
                                  :}
                                  | STRING_TYPE:value
                                  {:
                                        RESULT = SemanticAnalyzer.STRING_TYPE;
                                  :}
                                  | CHAR_TYPE:value
                                  {:
                                        RESULT = SemanticAnalyzer.CHAR_TYPE;
                                  :}
                                  | IDENTIFIER:name
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().isVariableOrMethodParameterOrFieldInCurrentClassDeclared(name);
                                  :}
                                  | NEW_KEYWORD IDENTIFIER:className LEFT_PARENTHESIS RIGHT_PARENTHESIS
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().isClassDeclared(className);
                                  :}
                                  | BREAK
                                  {:
                                       parser.getSemanticAnalyzer().breakExpression();
                                  :}
                                  | MINUS expression:arg
                                  {:
                                        RESULT = parser.getSemanticAnalyzer().unaryMinus(arg);
                                  :}
                                    %prec UNARY_MINUS;

actual_params_list              ::= actual_params_list:list COMMA expression:expression
                                  {:
                                       RESULT = list;
                                       RESULT.add(expression);
                                  :}
                                  | expression:expression
                                  {:
                                       RESULT = new LinkedList<String>();
                                       RESULT.add(expression);
                                  :}
                                  ;
